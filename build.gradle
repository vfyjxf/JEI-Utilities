buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
        maven {
            name = "github"
            url = "https://github.com/juanmuscaria/maven/raw/master"
        }
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = mod_version
group = mod_group
archivesBaseName = mod_archives_name + "-" + mc_version

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    version = "1.12.2-14.23.5.2847"
    mappings = "stable_39"
    runDir = "run"

    replace '@VERSION@', project.version

    def args = [
            "-Dfml.coreMods.load=com.github.vfyjxf.jeiutilities.asm.JeiUtilitiesLoadingPlugin",
            "-Dmixin.hotSwap=true",
            "-Dmixin.checks.interfaces=true",
            "-Dmixin.debug.export=true"
    ]

    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
}

processResources {

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version,
                'mod_id': mod_id,
                'mod_name': mod_name,
                'mod_author': mod_author,
                'mod_description': mod_description,
                'mc_version': mc_version,
                'mod_version': mod_version,
                'mod_icon': mod_icon
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        url = 'https://maven.blamejared.com'
        name = 'BlameJared Maven'
    }
    maven {
        name = "CurseMaven"
        url = "https://cursemaven.com"
    }
    maven {
        url "https://maven.cleanroommc.com"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
    //big recipe test
    runtime "curse.maven:extended-crafting-268387:2777071"
    runtime "curse.maven:cucumber-272335:2645867"
//    deobfCompile("zone.rong:mixinbooter:4.2")
}

task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

runClient {
    args '--username', 'developer'
}

jar {
    manifest.attributes(
            'FMLCorePlugin': 'com.github.vfyjxf.jeiutilities.asm.JeiUtilitiesLoadingPlugin',
            'FMLCorePluginContainsFMLMod': 'true'
    )
}

artifacts {
    archives devJar
    archives sourcesJar
}